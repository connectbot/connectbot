buildscript {
    repositories {
        google()
        jcenter()
        google()
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.2'
        classpath 'com.android.tools.build:gradle:3.1.2'
        classpath 'com.github.gfx.ribbonizer:ribbonizer-plugin:2.1.0'
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.3'
        if (rootProject.hasProperty('jacocoVersion')) {
            classpath "org.jacoco:org.jacoco.core:${rootProject.jacocoVersion}"
        }
    }
}

plugins {
    id 'info.vividcode.android.sdk-manager' version '0.9.0'
}

ext {
    supportLibraryVersion = '27.0.2'
    testRunnerVersion = '0.5'
    espressoVersion = '2.2.2'
}

apply plugin: 'info.vividcode.android.sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.github.gfx.ribbonizer'
apply plugin: 'jacoco-android'
apply from: '../config/quality.gradle'
apply from: '../config/translations.gradle'

repositories {
    google()
    jcenter()
    maven {
        url 'https://oss.jfrog.org/artifactory/oss-snapshot-local/'
    }
}

def isCi = "true".equals(System.getenv("CI"))
def preDexEnabled = "true".equals(System.getProperty("pre-dex", "true"))

coveralls.jacocoReportPath = 'build/reports/coverage/google/debug/report.xml'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    dexOptions {
        // Skip pre-dexing when running on a CI or when disabled via -Dpre-dex=false
        preDexLibraries = preDexEnabled && !isCi
    }

    defaultConfig {
        applicationId "org.connectbot"
        versionName "1.9.2"
        versionCode 19200

        minSdkVersion 14
        targetSdkVersion 27

        vectorDrawables.useSupportLibrary = true

        ndk {
            abiFilters 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
        }

        testApplicationId "org.connectbot.tests"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        if (project.hasProperty('keystorePassword')) {
            release {
                storeFile file(property('keystoreFile'))
                storePassword property('keystorePassword')
                keyAlias property('keystoreAlias')
                keyPassword property('keystorePassword')
            }
        }
    }

    dependencies {
        implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
        implementation "com.android.support:support-v4:$supportLibraryVersion"
        implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
        implementation "com.takisoft.fix:preference-v7:$supportLibraryVersion.0"
        implementation "com.android.support:design:$supportLibraryVersion"

        androidTestImplementation("com.android.support.test:runner:$testRunnerVersion") {
            exclude module: "support-annotations"
        }
        androidTestImplementation("com.android.support.test:rules:$testRunnerVersion") {
            exclude module: "support-annotations"
        }
        androidTestImplementation("com.android.support.test.espresso:espresso-core:$espressoVersion") {
            exclude module: "support-annotations"
        }
        androidTestImplementation("com.android.support.test.espresso:espresso-intents:$espressoVersion") {
            exclude module: "support-annotations"
        }
        androidTestImplementation("com.android.support.test.espresso:espresso-contrib:$espressoVersion") {
            exclude group: 'com.android.support'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'

            if (project.hasProperty('keystorePassword')) {
                //noinspection GroovyAssignabilityCheck
                signingConfig signingConfigs.release
            }

            return true // this silences Android Studio's groovy inspector
        }

        debug {
            applicationIdSuffix ".debug"
            testCoverageEnabled true
        }
    }

    flavorDimensions "license"

    productFlavors {
        // This product flavor uses the Conscrypt library which is open
        // source and licensed under Apache 2.
        oss {
            dimension "license"
            versionNameSuffix "-oss"
            ndk {
                abiFilters "armeabi-v7a", "arm64-v8a", "x86", "x86_64"
            }
        }

        // This product flavor uses the Google Play Services library for
        // ProviderInstaller. It uses Conscrypt under-the-hood, but the
        // Google Play Services SDK itself is not open source.
        google {
            dimension "license"
            versionNameSuffix ""
        }
    }

    lintOptions {
        abortOnError false
        lintConfig file('lint.xml')
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'LICENSE.txt'
    }
    externalNativeBuild {
        cmake {
            path 'CMakeLists.txt'
        }
    }
}

// Dependencies must be below the android block to allow productFlavor specific deps.
dependencies {
    implementation 'org.connectbot:sshlib:2.2.5'
    googleImplementation 'com.google.android.gms:play-services-base:11.8.0'
    ossImplementation 'org.conscrypt:conscrypt-android:1.1.2'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.13.0'
    testImplementation 'org.assertj:assertj-core:3.9.0'
    testImplementation('org.robolectric:robolectric:3.6.1') {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
}

def getGitDescription = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--dirty'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (ignored) {
        return null;
    }
}

// Insert the build number into strings.xml
android.applicationVariants.all { variant ->
    variant.mergeResources.doLast{
        ext.env = System.getenv()
        def buildNumber = getGitDescription()
        if (buildNumber != null) {
            File valuesFile = file("${buildDir}/intermediates/incremental/${variant.mergeResources.name}/merged.dir/values/values.xml")
            String content = valuesFile.getText('UTF-8')
            content = content.replaceAll(/\(working copy\)/, buildNumber)
            valuesFile.write(content, 'UTF-8')
        }
    }
}

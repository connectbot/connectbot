pluginManagement {
	def MAVEN_REPO_CACHE = System.getenv("MAVEN_REPO_CACHE")?.trim()
	if (MAVEN_REPO_CACHE) {
		repositories {
			maven {
				url MAVEN_REPO_CACHE
			}
		}
	} else {
		repositories {
			gradlePluginPortal()
			google()
		}
	}

	resolutionStrategy {
		eachPlugin {
			if (requested.id.id == "com.android.application") {
				useModule("com.android.tools.build:gradle:${requested.version}")
			}
		}
	}
}

ext.MAVEN_REPO_CACHE = System.getenv("MAVEN_REPO_CACHE")?.trim()
ext.GRADLE_BUILD_CACHE = System.getenv("GRADLE_BUILD_CACHE")?.trim()
ext.TRANSLATIONS_ONLY = System.getenv("TRANSLATIONS_ONLY")?.trim()

buildCache {
	local { enabled = !GRADLE_BUILD_CACHE }
	if (GRADLE_BUILD_CACHE) {
		remote(HttpBuildCache) {
			url = GRADLE_BUILD_CACHE
			push = true
		}
	}
}

if (!TRANSLATIONS_ONLY) {
	include ':app'
}
include ':translations'
